/*
â–ˆâ–ˆâ•—     â–ˆâ–ˆâ•—   â–ˆâ–ˆâ•— â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•— â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•—  â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•— â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•—
â–ˆâ–ˆâ•‘     â–ˆâ–ˆâ•‘   â–ˆâ–ˆâ•‘â–ˆâ–ˆâ•”â•â•â•â•â•â•—â–ˆâ–ˆâ•”â•â•â–ˆâ–ˆâ•—â–ˆâ–ˆâ•”â•â•â–ˆâ–ˆâ•—â–ˆâ–ˆâ•”â•â•â•â•â•
â–ˆâ–ˆâ•‘     â–ˆâ–ˆâ•‘   â–ˆâ–ˆâ•‘â–ˆâ–ˆâ•‘     â•‘â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•”â•â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•‘â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•—
â–ˆâ–ˆâ•‘     â–ˆâ–ˆâ•‘   â–ˆâ–ˆâ•‘â–ˆâ–ˆâ•‘     â•‘â–ˆâ–ˆâ•”â•â•â–ˆâ–ˆâ•—â–ˆâ–ˆâ•”â•â•â–ˆâ–ˆâ•‘â•šâ•â•â•â•â–ˆâ–ˆâ•‘
â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•—â•šâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•”â•â•šâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•”â•â–ˆâ–ˆâ•‘  â–ˆâ–ˆâ•‘â–ˆâ–ˆâ•‘  â–ˆâ–ˆâ•‘â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ•‘
â•šâ•â•â•â•â•â•â• â•šâ•â•â•â•â•â•  â•šâ•â•â•â•â•â• â•šâ•â•  â•šâ•â•â•šâ•â•  â•šâ•â•â•šâ•â•â•â•â•â•â•
-----------------------------------------------------------
ğŸ“Œ Autor: Lucas Galindo
ğŸ”— GitHub: https://github.com/Lucras22
ğŸ“§ E-mail: devlucasgalindo@email.com
ğŸ’¼ LinkedIn: https://www.linkedin.com/in/lucasgalindoiot/
-----------------------------------------------------------
ğŸ“œ InstruÃ§Ãµes de Uso:
- 
-----------------------------------------------------------
ğŸ“‚ RepositÃ³rio do Projeto:
ğŸ”— https://github.com/Lucras22/Estufa_Aquacultura_Hidroponia_Salobra
-----------------------------------------------------------
ğŸ› ï¸ LicenÃ§a: ....
*/

// ##############  BIBLIOTECAS
#include "DHT.h"
#include <OneWire.h>
#include <DallasTemperature.h>
#include <WiFi.h>
#include <HTTPClient.h>

// ##############  CONFIGURAÃ‡ÃƒO DO WIFI
const char* ssid = "Lucas Galindo | POCO C65";       // Substitua pelo seu Wi-Fi
const char* password = "lucras22";  // Substitua pela senha

// ##############  CONFIGURAÃ‡ÃƒO DO TELEGRAM
const String botToken = "7751526303:AAEjh5i6E2B0uGwTbU3TGWbjhbvcdTEvFdg";  // Substitua pelo token do seu bot
const String chatId = "7003158288";     // Substitua pelo seu chat ID

WiFiClientSecure client;

// FunÃ§Ã£o para enviar mensagem para o Telegram via POST
void sendMessage(String message) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    String url = "https://api.telegram.org/bot" + botToken + "/sendMessage";
    
    http.begin(url);
    http.addHeader("Content-Type", "application/x-www-form-urlencoded");
    
    // Montando os parÃ¢metros da requisiÃ§Ã£o POST
    String postData = "chat_id=" + chatId + "&text=" + message;
    
    // Enviando a requisiÃ§Ã£o
    int httpResponseCode = http.POST(postData);
    
    if (httpResponseCode > 0) {
      String response = http.getString();
      Serial.println("CÃ³digo de resposta: " + String(httpResponseCode));
      Serial.println("Resposta: " + response);
    } else {
      Serial.print("Erro na requisiÃ§Ã£o: ");
      Serial.println(httpResponseCode);
    }
    
    http.end();
  } else {
    Serial.println("WiFi desconectado");
  }
}

// ##############  PINOS
#define DHT1PIN 14  //Interno
#define DHT2PIN 13  //Externo
#define tdsPin 33   //Sensor TDS
#define ONE_WIRE_BUS 4 //Sensor Temp Agua
#define PH_SENSOR_PIN 34 // Sensor pH

// ##############  DHT GLOBAL
#define DHTTYPE DHT22
DHT dht1(DHT1PIN, DHTTYPE);
DHT dht2(DHT2PIN, DHTTYPE);

// ##############  TEMP AGUA GLOBAL
OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature sensors(&oneWire);

// ##############  SENSOR PH
float calibracao_ph7 = 1.65;
float calibracao_ph4 = 1.35;
float calibracao_ph10 = 2.05;
float m_4_7, b_4_7, m_7_10, b_7_10;

void setup() {
  Serial.begin(115200);
  pinMode(tdsPin, INPUT);
  
  dht1.begin();
  dht2.begin();
  sensors.begin();

  m_4_7 = (4.0 - 7.0) / (calibracao_ph4 - calibracao_ph7);
  b_4_7 = 7.0 - m_4_7 * calibracao_ph7;
  m_7_10 = (7.0 - 10.0) / (calibracao_ph7 - calibracao_ph10);
  b_7_10 = 10.0 - m_7_10 * calibracao_ph10;

  // Conectar ao Wi-Fi
  WiFi.begin(ssid, password);
  Serial.print("Conectando ao Wi-Fi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nWiFi conectado!");
}

void loop() {
  // Leitura do sensor de pH
  int buf[10];
  for (int i = 0; i < 10; i++) {  
    buf[i] = analogRead(PH_SENSOR_PIN);
    delay(10);
  }

  // OrdenaÃ§Ã£o e mÃ©dia das amostras centrais
  for (int i = 0; i < 9; i++) {
    for (int j = i + 1; j < 10; j++) {
      if (buf[i] > buf[j]) {
        int temp = buf[i];
        buf[i] = buf[j];
        buf[j] = temp;
      }
    }
  }

  int valorMedio = 0;
  for (int i = 2; i < 8; i++) {
    valorMedio += buf[i];
  }

  float tensao = (valorMedio * 3.3) / 4095.0 / 6;
  float ph = (tensao < calibracao_ph7) ? (m_4_7 * tensao + b_4_7) : (m_7_10 * tensao + b_7_10);

  // Leitura dos sensores DHT
  float humidity1 = dht1.readHumidity();
  float temperature1 = dht1.readTemperature();
  float humidity2 = dht2.readHumidity();
  float temperature2 = dht2.readTemperature();

  if (isnan(humidity1) || isnan(temperature1) || isnan(humidity2) || isnan(temperature2)) {
    Serial.println("Falha ao ler DHT!");
    return;
  }

  // Leitura do TDS
  int tdsValue = analogRead(tdsPin);
  float conductivity = tdsValue * 2;

  // Leitura da temperatura da Ã¡gua
  sensors.requestTemperatures();
  float temperatureWater = sensors.getTempCByIndex(0);

 //EXIBIR MONITOR SERIAL

  Serial.print("Umidade Interna: ");
  Serial.print(humidity1);
  Serial.print(" %\t");
  Serial.print("Temperatura Interna: ");
  Serial.print(temperature1);
  Serial.println(" Â°C");

  Serial.print("Umidade Externa: ");
  Serial.print(humidity2);
  Serial.print(" %\t");
  Serial.print("Temperatura Externa: ");
  Serial.print(temperature2);
  Serial.println(" Â°C");

  Serial.print("Temperatura Agua: ");
  Serial.print(temperatureWater);
  Serial.println(" C");

  Serial.print("TDS Valor (PPM): ");
  Serial.println(tdsValue);
  Serial.print("Condutividade Eletrica (ECC): ");
  Serial.println(conductivity);

  Serial.print("TensÃ£o medida: ");
  Serial.print(tensao, 3);
  Serial.print(" V | pH: ");
  Serial.println(ph, 2);

  // Enviar dados para o Telegram
  String mensagem = " Dados dos Sensores:\n\n";
  mensagem += " Interno: " + String(temperature1) + "Â°C | " + String(humidity1) + "%\n";
  mensagem += " Externo: " + String(temperature2) + "Â°C | " + String(humidity2) + "%\n";
  mensagem += " Temp. Ãgua: " + String(temperatureWater) + "Â°C\n";
  mensagem += " TDS: " + String(tdsValue) + " ppm\n";
  mensagem += " Condutividade: " + String(conductivity) + "ECC\n";
  mensagem += " pH: " + String(ph, 2) + "\n";

  sendMessage(mensagem);  // Envia para o Telegram

  // Esperar 5 segundos antes da prÃ³xima leitura
  delay(5000);
}
